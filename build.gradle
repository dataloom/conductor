/*
*	Kryptnostic conductor service
* 	version is defined in kryptnostic-gradles/kryptnostic.gradle
*/

description = "Kryptnostic Conductor Server"
group = "com.kryptnostic"
ext.projectName = "kryptnostic-conductor"

apply plugin: "application"
apply plugin: "java"
apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "jacoco"

apply from: "http://api.krypt.int/gradle/kryptnostic.gradle"

mainClassName = "com.kryptnostic.conductor.Conductor"
applicationDefaultJvmArgs = ["-Xmx1G","-Xms1G","-server"]
sourceCompatibility=JavaVersion.VERSION_1_8
targetCompatibility=JavaVersion.VERSION_1_8

run {
  if (System.getProperty("exec.args")) {
    args System.getProperty("exec.args").split()
  }
}

configurations {
    provided
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}

ext.spark_version='1.6.0'
ext.spark_cassandra_connector_version='1.6.0-M1'
ext.jackson_version='2.6.5'
dependencies {  
    compile  "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:${jackson_version}"
    compile "com.datastax.spark:spark-cassandra-connector-java_2.11:${spark_cassandra_connector_version}"
    compile "org.apache.spark:spark-core_2.11:${spark_version}"
    compile "org.apache.spark:spark-sql_2.11:${spark_version}"
    compile "org.apache.spark:spark-mllib_2.11:${spark_version}"
    compile "org.apache.spark:spark-graphx_2.11:${spark_version}"
    compile "org.apache.spark:spark-streaming_2.11:${spark_version}"

    if( project.hasProperty('developmentMode') && project.developmentMode ) {
        logger.quiet(project.name + " using project dependencies.")
        compile project(":rhizome")
        compile project(":kryptnostic-mapstores")
        compile project(":kryptnostic-conductor-client")
    } else {
        logger.quiet(project.name + " using jar dependencies.")
        compile "com.kryptnostic:rhizome:${rhizome_version}"
        compile "com.kryptnostic:kryptnostic-mapstores:${mapstores_version}"
        compile "com.kryptnostic:kryptnostic-conductor-client:${conductor_client_version}"
    }
    /*
     * TESTING
     */
     testCompile "junit:junit:${junit_version}"
     testCompile "org.mockito:mockito-all:${mockito_version}"

}

test {
    systemProperty 'java.library.path','lib/centos:lib/macosx:/usr/lib64'
    jvmArgs '-Djava.library.path=./lib/centos:./lib/macosx:/usr/lib64'
}

task sourcesJar(type : Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

eclipse {
    ext.downloadSources = true
    ext.downloadJavadoc = true

    ext.sourceCompatibility=JavaVersion.VERSION_1_8
    ext.targetCompatibility=JavaVersion.VERSION_1_8
}

eclipse.classpath.plusConfigurations += [configurations.provided] 